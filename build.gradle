plugins {
    id "java-platform"
}

allprojects {
    apply plugin: "maven-publish"

    base.archivesName = "${rootProject.archives_base_name}-${project.name}"
    version = rootProject.mod_version
    group = rootProject.maven_group

    repositories {
        maven {
            name = "Redlance"
            url = "https://repo.redlance.org/public"
        }
        mavenCentral()
    }

    tasks.withType(JavaCompile).configureEach {
        options.encoding = "UTF-8"
        options.release.set 25
    }

    publishing.repositories {
        mavenLocal()
    }
}

subprojects {
    apply plugin: "java"
    apply plugin: "java-library"

    dependencies {
        compileOnlyApi("org.jetbrains:annotations:26.0.2")

        testImplementation("org.junit.jupiter:junit-jupiter-api:6.0.1")
        testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine:6.0.1")
        testRuntimeOnly("org.junit.platform:junit-platform-launcher:6.0.1")
    }

    test {
        useJUnitPlatform()
        options {
            systemProperties(System.getProperties())
        }
    }

    java {
        withSourcesJar()
    }

    publishing.publications {
        mavenJava(MavenPublication) {
            artifactId = "${rootProject.archives_base_name}-${project.name}"
            from components.java
        }
    }
}

publishing.publications {
    register('mavenJava', MavenPublication) {
        from(components['javaPlatform'])
    }
}

tasks.withType(GenerateModuleMetadata).configureEach {
    enabled = true
}

dependencies {
    constraints {
        for (proj in rootProject.subprojects) {
            api(project(proj.path))
        }
    }
}
